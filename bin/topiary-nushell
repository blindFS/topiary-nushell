#!/usr/bin/env -S nu --stdin

const repo_path = path self ..

# Wrapper for `topiary` that formats `nushell` files
@example "Read from stdin" { bat foo.nu | topiary-nushell }
@example "Format files (in-place replacement)" { topiary-nushell foo.nu bar.nu }
@example "Path overriding" { topiary-nushell -c /path/to/topiary-nushell-repo foo.nu bar.nu }
def main [
  --config_dir (-c): path # Root of the topiary-nushell repo, defaults to the directory one level up from this script
  ...files: path # Files to format
]: [nothing -> nothing string -> string] {
  let config_dir = $config_dir | default $repo_path
  $env.TOPIARY_CONFIG_FILE = ($config_dir | path join languages.ncl)
  $env.TOPIARY_LANGUAGE_DIR = ($config_dir | path join languages)

  if ($files | is-not-empty) {
    topiary format ...$files
  } else {
    topiary format --language nu
  }
}
